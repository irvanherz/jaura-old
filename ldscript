/*ENTRY(MultibootEntry)*/
KRN_BASE_VMA = 0x80000000;
KRN_BASE_LMA = 0x01000000;
SECTIONS
{
	. = KRN_BASE_VMA;
	.text ALIGN(0x1000) :
	AT(ADDR(.text) - KRN_BASE_VMA + KRN_BASE_LMA)
	{
		KRN_TEXT_BASE_VMA = .;
		KRN_TEXT_BASE_LMA = . - KRN_BASE_VMA + KRN_BASE_LMA;
		KInitMultiboot.*(.*)
		KInit*(.*)
		*(.text*)
	}
	.data ALIGN(0x1000) :
	{
		KRN_DATA_BASE_LMA = . - KRN_BASE_VMA + KRN_BASE_LMA;
		KRN_DATA_BASE_VMA = .;
		*(.data*)
		*(.rdata*)
		*(.rodata*)
	}
	
	.bss ALIGN(0x1000) :
	{
		*(.bss)
		KRN_BSS_END_LMA = . - KRN_BASE_VMA + KRN_BASE_LMA;
		KRN_BSS_END_VMA = .;
	}
	
	/DISCARD/ :
	{
		*(.*)
	}
	KRN_END_VMA = .;
	KRN_END_LMA = . - KRN_BASE_VMA + KRN_BASE_LMA;
} 